@Metadata.layer: #CORE
@UI: {
  headerInfo: { typeName: 'Market',
                typeNamePlural: 'Markets',
                imageUrl: '_MarketDesc.Imageurl'
               }
     }          


annotate view ZSA_C_PROD_MRKT
    with 
{
 @UI.facet: [ 
             { id: 'GenInfo',
               type: #COLLECTION,
               label: 'General Information',
               position: 10               
             },
             { id: 'AdminData',
               type: #COLLECTION,
               label: 'Admin Data',
               position: 20               
             },
             { id: 'OrderQuantity',
               purpose: #STANDARD,
               type:    #CHART_REFERENCE,
               label:   'Order Quantity',
               targetElement: '_OrderQantity',
               position: 40               
             },
             { id: 'OrderAmount',
               purpose: #STANDARD, 
               type: #CHART_REFERENCE,
               label: 'Order Amount',
               targetElement: '_OrderAmount',
               position: 50               
             },                
             { id:            'BasicData',
               purpose:       #STANDARD ,
               parentId:      'GenInfo',
               type:          #FIELDGROUP_REFERENCE,
               label:         'Basic Data',
               position:       10,
               targetQualifier: 'BasicData' },
             { id:            'CreateInf',
               purpose:       #STANDARD ,
               parentId:      'AdminData',
               type:          #FIELDGROUP_REFERENCE,
               label:         'Create Info',
               position:       10,
               targetQualifier: 'CreateInf' },
              { id:             'ChangeInf',
               purpose:       #STANDARD ,
               parentId:      'AdminData',
               type:          #FIELDGROUP_REFERENCE,
               label:         'Change Info',
               position:       20,
               targetQualifier: 'ChangeInf' },
                { id:            'Order',
                purpose:         #STANDARD,
                type:            #LINEITEM_REFERENCE,
                label:           'Order',
                position:        30,
                targetElement:   '_Order'}  ]


@UI.identification: [
    { type: #FOR_ACTION, dataAction:'confirm', label: 'Confirm', position: 10 } ] 


   
@UI.hidden: true
ProdUuid;
@UI.hidden: true
MrktUuid;
@UI: {
    lineItem: [ { position: 10, importance: #HIGH, label: 'Market' } ],
    fieldGroup: [ { qualifier: 'BasicData',
                   position: 20,
                   label: 'Market' } ],
//    dataPoint: { title: 'Country' },
    identification: [{position: 10 }]                  
    }
Mrktid;
@UI.dataPoint: { title: 'Country' }
Mrktname;
@UI: {
    lineItem: [ { position: 20, importance: #HIGH, label: 'Confirmed?', criticality: 'Criticality' } ],
    fieldGroup: [ { qualifier: 'BasicData',
                   position: 30,
                   label: 'Confirmed?',
                   criticality: 'Criticality' } ] 
    }
Status;
@UI: { 
        lineItem: [ { position: 30, importance: #HIGH, label: 'Start date' } ],
        fieldGroup: [ { qualifier: 'BasicData',
                        position: 40,
                        label: 'Start Date' } ],
        dataPoint: { title: 'Start Date' }                   
     }
Startdate;
@UI: {
        lineItem: [ { position: 40, importance: #HIGH, label: 'End date' } ],
        fieldGroup: [ { qualifier: 'BasicData',
                        position: 50,
                        label: 'End Date' } ],
        dataPoint: { title: 'End Date' }                   
     }   
Enddate;
@UI: {
        lineItem: [ { position: 50, importance: #HIGH, label: 'Total Quantity' } ]}
TotalQuantity;
@UI: {
        lineItem: [ { position: 60, importance: #HIGH, label: 'Total Net Amount' } ]}
TotalNetamount;
@UI: {
        lineItem: [ { position: 70, importance: #HIGH, label: 'Total Gross Amount' } ]}
TotalGrossamount;

@UI.hidden: true
amountcurr;

@UI: {  
       fieldGroup: [ { qualifier: 'CreateInf',
                       position: 10 } ] 
     }
CreatedBy;
@UI: {  
       fieldGroup: [ { qualifier: 'CreateInf',
                       position: 20, label: 'Created On' } 
                        ] 
     }
CreationTime_Date;
@UI.hidden: true
CreationTime;
@UI: {  
       fieldGroup: [ { qualifier: 'ChangeInf',
                       position: 10 } ] 
     }
ChangedBy;

@UI.hidden: true     
ChangeTime;
@UI: {  
       fieldGroup: [ { qualifier: 'ChangeInf',
                       position: 20, label: 'Changed On' } ] 
     }
ChangeTime_Date;    
}
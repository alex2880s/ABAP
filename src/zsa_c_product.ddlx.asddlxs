@Metadata.layer: #CORE
@UI: {
  headerInfo: { typeName: 'Kitchen Applience',
                typeNamePlural: 'Kitchen Appliences',
                imageUrl: '_ProductDesc.Imageurl'
               },
   presentationVariant: [{ sortOrder: [{ by: 'Prodid', direction:  #ASC }, { by: 'Pgid', direction:  #ASC }] }]              
     }  
annotate view ZSA_C_PRODUCT
    with 
{
 @UI.facet: [ 
             { id: 'GenInfo',
               type: #COLLECTION,
               label: 'General Information',
               position: 10               
             },
             { id: 'AdminData',
               type: #COLLECTION,
               label: 'Admin Data',
               position: 20               
             },
             { id: 'MarketQuantity',
               purpose: #STANDARD,
               type:    #CHART_REFERENCE,
               label: 'Market Quantity',
               targetElement: '_QuanChart',
               position: 40               
             }, 
             { id: 'MarketAmount',
               purpose: #STANDARD,
               type:    #CHART_REFERENCE,
               label: 'Market Amount',
               targetElement: '_AmoChart',
               position: 50               
             },               
             { id:            'BasicData',
               purpose:       #STANDARD ,
               parentId:      'GenInfo',
               type:          #FIELDGROUP_REFERENCE,
               label:         'Basic Data',
               position:       10,
               targetQualifier: 'BasicData' },                
             { id:            'SizeDim',
               purpose:       #STANDARD ,
               parentId:      'GenInfo',
               type:          #FIELDGROUP_REFERENCE,
               label:         'Size Dimensions',
               position:       20,
               targetQualifier: 'SizeDim' },                
             { id:            'PriceDet',
               purpose:       #STANDARD ,
               parentId:      'GenInfo',
               type:          #FIELDGROUP_REFERENCE,
               label:         'Price Details',
               position:       30,
               targetQualifier: 'PriceDet' },
             { id:            'CreateInf',
               purpose:       #STANDARD ,
               parentId:      'AdminData',
               type:          #FIELDGROUP_REFERENCE,
               label:         'Create Info',
               position:       10,
               targetQualifier: 'CreateInf' },
              { id:            'ChangeInf',
               purpose:       #STANDARD ,
               parentId:      'AdminData',
               type:          #FIELDGROUP_REFERENCE,
               label:         'Change Info',
               position:       20,
               targetQualifier: 'ChangeInf' },
               { id:              'Market',
                purpose:         #STANDARD,
                type:            #LINEITEM_REFERENCE,
                label:           'Market',
                position:        30,
                targetElement:   '_Market'}                   
            ]
@UI.lineItem: [
    { type: #FOR_ACTION, dataAction:'moveToNextPhase', label: 'Move', position: 10 },
    { type: #FOR_ACTION, dataAction:'copyProduct',     label: 'Copy', position: 20 }]
@UI.identification: [
    { type: #FOR_ACTION, dataAction:'moveToNextPhase', label: 'Move', position: 10 } ] 
                 
@UI.hidden: true
ProdUuid;
@UI: { 
       selectionField: [{position: 10}] ,
       lineItem: [ { position: 10, importance: #HIGH } ],
       fieldGroup: [ { qualifier: 'BasicData',
                       position: 10 } ] ,
       dataPoint: { title: 'Product ID' }                
     }
Prodid;
@UI: { 
       selectionField: [{position: 30}] , 
       lineItem: [ { position: 20, importance: #HIGH, label: 'Product Group' } ],
       fieldGroup: [ { qualifier: 'BasicData',
                       position: 20,
                       label: 'Product Group' } ] 
     }
Pgid;
@UI: { 
       
       selectionField: [{position: 20}] , 
       lineItem: [ { position: 30, importance: #HIGH, label: 'Phase', criticality: 'Criticality'  } ],
       fieldGroup: [ { qualifier: 'BasicData',
                       position: 30, 
                       label: 'Phase',
                       criticality: 'Criticality' }] 
     }
Phaseid;

@UI: { 
       selectionField: [{position: 40}] ,
       lineItem: [ { position: 40, importance: #HIGH, label:'Net Price' } ],
       fieldGroup: [ { qualifier: 'PriceDet',
                       position: 10,
                       label:'Net Price' } ],
       dataPoint: {title: 'Net Price' }                
     }
Price;

@UI: { 
       lineItem: [ { position: 50, importance: #HIGH } ],
       fieldGroup: [ { qualifier: 'PriceDet',
                       position: 20 } ],
       dataPoint: { title: 'Tax Rate' }                 
     }
Taxrate;
@UI: { 
       lineItem: [ { position: 60, importance: #HIGH, label: 'Ext. Price' } ],
       fieldGroup: [ { qualifier: 'PriceDet',
                       position: 20,
                       label: 'External Price' } ],
       dataPoint: { title: 'External Price' }                 
     }
ExtPrice;
@UI: { 
       lineItem: [ { position: 70, importance: #HIGH } ],
       fieldGroup: [ { qualifier: 'SizeDim',
                       position: 10 } ] 
     }
Depth;
@UI: { 
       lineItem: [ { position: 80, importance: #HIGH } ],
       fieldGroup: [ { qualifier: 'SizeDim',
                       position: 20 } ] 
     }       
Height;
@UI: { 
       lineItem: [ { position: 90, importance: #HIGH } ],
       fieldGroup: [ { qualifier: 'SizeDim',
                       position: 30 } ] 
     }
Width;

@UI: { identification: [{position: 10 }] }
SizeUom;
@UI: {  
       fieldGroup: [ { qualifier: 'CreateInf',
                       position: 10,
                       label: 'Created By' } ] 
     }
CreatedBy;
@UI: {  
       fieldGroup: [ { qualifier: 'CreateInf',
                       position: 20,
                       label: 'Create Time' } ] 
     }
CreationTime;
@UI: {  
       fieldGroup: [ { qualifier: 'ChangeInf',
                       position: 10,
                       label: 'Changed By' } ]                 
     }
ChangedBy;
@UI: {  
       fieldGroup: [ { qualifier: 'ChangeInf',
                       position: 20,
                       label: 'Change Time' } ] 
     }
ChangeTime;
@UI.hidden: true
Phase;
}